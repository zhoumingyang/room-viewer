#version 300 es
precision highp float;
precision highp int;
precision highp sampler2D;

#include <pathtracing_uniforms_and_defines>

uniform sampler2D tTriangleTexture;
uniform sampler2D tAABBTexture;
uniform sampler2D tAlbedoTextures[8]; // 8 = max number of diffuse albedo textures per model

#define INV_TEXTURE_WIDTH 0.00048828125
#define N_DISKS 1
#define N_SPHERES 3
#define N_BOXES 2
#define N_OPENCYLINDERS 1

//-----------------------------------------------------------------------
struct Ray { vec3 origin; vec3 direction; };
struct Disk { float radius; vec3 pos; vec3 normal; vec3 emission; vec3 color; int type; };
struct Sphere { float radius; vec3 position; vec3 emission; vec3 color; int type; };
struct OpenCylinder { vec3 pos0; vec3 pos1; float radius; vec3 emission; vec3 color; int type; };
struct Box { vec3 minCorner; vec3 maxCorner; vec3 emission; vec3 color; int type; };
struct Intersection { vec3 normal; vec3 emission; vec3 color; vec2 uv; int type; int albedoTextureID; };

Disk disks[N_DISKS];
Sphere spheres[N_SPHERES];
OpenCylinder openCylinders[N_OPENCYLINDERS];
Box boxes[N_BOXES];

#include <pathtracing_random_functions>
#include <pathtracing_calc_fresnel_reflectance>
#include <pathtracing_disk_intersect>
#include <pathtracing_sphere_intersect>
#include <pathtracing_opencylinder_intersect>
#include <pathtracing_box_intersect>
#include <pathtracing_boundingbox_intersect>
#include <pathtracing_bvhTriangle_intersect>
#include <pathtracing_sample_sphere_light>

struct StackLevelData {
	float id;
    float rayT;
} stackLevels[24];

struct BoxNode {
	float branch_A_Index;
	vec3 minCorner;
	float branch_B_Index;
	vec3 maxCorner;  
};

BoxNode GetBoxNode(const in float i) {
	// each bounding box's data is encoded in 2 rgba(or xyzw) texture slots 
	float iX2 = (i * 2.0);
	// (iX2 + 0.0) corresponds to .x: idLeftChild, .y: aabbMin.x, .z: aabbMin.y, .w: aabbMin.z 
	// (iX2 + 1.0) corresponds to .x: idRightChild .y: aabbMax.x, .z: aabbMax.y, .w: aabbMax.z 

	ivec2 uv0 = ivec2( mod(iX2 + 0.0, 2048.0), floor((iX2 + 0.0) * INV_TEXTURE_WIDTH) );
	ivec2 uv1 = ivec2( mod(iX2 + 1.0, 2048.0), floor((iX2 + 1.0) * INV_TEXTURE_WIDTH) );
	
	vec4 aabbNodeData0 = texelFetch(tAABBTexture, uv0, 0);
	vec4 aabbNodeData1 = texelFetch(tAABBTexture, uv1, 0);

	BoxNode BN = BoxNode( aabbNodeData0.x,
			      aabbNodeData0.yzw,
			      aabbNodeData1.x,
			      aabbNodeData1.yzw );
    return BN;
}

//----------------------------------------------------------
float SceneIntersect( Ray r, inout Intersection intersec ) {
	vec3 n;
	float d = INFINITY;
	float t = INFINITY;
	
	// AABB BVH Intersection variables
	vec4 aabbNodeData0, aabbNodeData1, aabbNodeData2;
	vec4 vd0, vd1, vd2, vd3, vd4, vd5, vd6, vd7;
	vec3 aabbMin, aabbMax;
	vec3 inverseDir = 1.0 / r.direction;
	vec3 hitPos, toLightBulb;
	ivec2 uv0, uv1, uv2, uv3, uv4, uv5, uv6, uv7;

    float stackptr = 0.0;	
	float bc, bd;
	float id = 0.0;
	float tu, tv;
	float triangleID = 0.0;
	float triangleU = 0.0;
	float triangleV = 0.0;
	float triangleW = 0.0;
	
	bool skip = false;
	bool triangleLookupNeeded = false;

	BoxNode currentBoxNode, nodeA, nodeB, tnp;
	StackLevelData currentStackData, slDataA, slDataB, tmp;

	for (int i = 0; i < N_SPHERES; i++) {
		d = SphereIntersect( spheres[i].radius, spheres[i].position, r );
		if (d < t) {
			t = d;
			intersec.normal = (r.origin + r.direction * t) - spheres[i].position;
			intersec.emission = spheres[i].emission;
			intersec.color = spheres[i].color;
			intersec.type = spheres[i].type;
			intersec.albedoTextureID = -1;
			triangleLookupNeeded = false;
		}
	}
	
	for (int i = 0; i < N_BOXES; i++) {
		d = BoxIntersect( boxes[i].minCorner, boxes[i].maxCorner, r, n );
		if (d < t) {
			t = d;
			intersec.normal = normalize(n);
			intersec.emission = boxes[i].emission;
			intersec.color = boxes[i].color;
			intersec.type = boxes[i].type;
			intersec.albedoTextureID = -1;
			triangleLookupNeeded = false;
		}
	}
	
	d = DiskIntersect( disks[0].radius, disks[0].pos, disks[0].normal, r );
	if (d < t) {
		t = d;
		intersec.normal = dot(disks[0].normal,r.direction) <= 0.0 ? normalize(disks[0].normal) : normalize(disks[0].normal * -1.0);
		intersec.emission = disks[0].emission;
		hitPos = r.origin + r.direction * t;
		toLightBulb = normalize(spheres[1].position - hitPos);
		if (dot(intersec.normal, toLightBulb) > 0.0) {
			intersec.color = disks[0].color;
			intersec.type = disks[0].type;
		}
		else {
			intersec.color = vec3(0);
			intersec.type = DIFF;
		}
		intersec.albedoTextureID = -1;
		triangleLookupNeeded = false;
	}

	d = OpenCylinderIntersect( openCylinders[0].pos0, openCylinders[0].pos1, openCylinders[0].radius, r, n );
	if (d < t) {
		t = d;
		intersec.normal = normalize(n);
		intersec.emission = openCylinders[0].emission;
		hitPos = r.origin + r.direction * t;
		toLightBulb = normalize(spheres[1].position - hitPos);
		if (dot(intersec.normal, toLightBulb) > 0.0) {
			intersec.color = openCylinders[0].color;
			intersec.type = openCylinders[0].type;
		}
		else {
			intersec.color = vec3(0);
			intersec.type = DIFF;
		}
		intersec.albedoTextureID = -1;
		triangleLookupNeeded = false;
	}
	
	currentBoxNode = GetBoxNode(stackptr);
	currentStackData = StackLevelData(stackptr, BoundingBoxIntersect(currentBoxNode.minCorner, currentBoxNode.maxCorner, r.origin, inverseDir));
	stackLevels[0] = currentStackData;
	
	while (true) {
		if (currentStackData.rayT < t)  {
			//  < 0.0 signifies a leaf node
			if (currentBoxNode.branch_A_Index < 0.0) {
				// each triangle's data is encoded in 8 rgba(or xyzw) texture slots
				id = 8.0 * (-currentBoxNode.branch_A_Index - 1.0);

				uv0 = ivec2( mod(id + 0.0, 2048.0), floor((id + 0.0) * INV_TEXTURE_WIDTH) );
				uv1 = ivec2( mod(id + 1.0, 2048.0), floor((id + 1.0) * INV_TEXTURE_WIDTH) );
				uv2 = ivec2( mod(id + 2.0, 2048.0), floor((id + 2.0) * INV_TEXTURE_WIDTH) );
				
				vd0 = texelFetch(tTriangleTexture, uv0, 0);
				vd1 = texelFetch(tTriangleTexture, uv1, 0);
				vd2 = texelFetch(tTriangleTexture, uv2, 0);

				d = BVH_TriangleIntersect( vec3(vd0.xyz), vec3(vd0.w, vd1.xy), vec3(vd1.zw, vd2.x), r, tu, tv );

				if (d < t && d > 0.0) {
					t = d;
					triangleID = id;
					triangleU = tu;
					triangleV = tv;
					triangleLookupNeeded = true;
				}
			}
			else // else this is a branch
			{
				nodeA = GetBoxNode(currentBoxNode.branch_A_Index);
                nodeB = GetBoxNode(currentBoxNode.branch_B_Index);
                slDataA = StackLevelData(currentBoxNode.branch_A_Index, BoundingBoxIntersect(nodeA.minCorner, nodeA.maxCorner, r.origin, inverseDir));
                slDataB = StackLevelData(currentBoxNode.branch_B_Index, BoundingBoxIntersect(nodeB.minCorner, nodeB.maxCorner, r.origin, inverseDir));
				
				// first sort the branch node data so that 'a' is the smallest
				if (slDataB.rayT < slDataA.rayT) {
					tmp = slDataB;
					slDataB = slDataA;
					slDataA = tmp;

					tnp = nodeB;
					nodeB = nodeA;
					nodeA = tnp;
				} // branch 'b' now has the larger rayT value of 'a' and 'b'

				if (slDataB.rayT < t) // see if branch 'b' (the larger rayT) needs to be processed
				{
					currentStackData = slDataB;
					currentBoxNode = nodeB;
					skip = true; // this will prevent the stackptr from decreasing by 1
				}

				if (slDataA.rayT < t) // see if branch 'a' (the smaller rayT) needs to be processed 
				{
					if (skip == true) // if larger branch 'b' needed to be processed also,
						stackLevels[int(stackptr++)] = slDataB; // cue larger branch 'b' for future round
								// also, increase pointer by 1
					
					currentStackData = slDataA;
					currentBoxNode = nodeA;
					skip = true; // this will prevent the stackptr from decreasing by 1
				}
            }
		} // end if (currentStackData.rayT < t)

		if (skip == false) {
			// decrease pointer by 1 (0.0 is root level, 24.0 is maximum depth)
			if (--stackptr < 0.0) // went past the root level, terminate loop
				break;
            currentStackData = stackLevels[int(stackptr)];
            currentBoxNode = GetBoxNode(currentStackData.id);
        }
		skip = false; // reset skip
    } // end while (true)

	if (triangleLookupNeeded) {
		uv0 = ivec2( mod(triangleID + 0.0, 2048.0), floor((triangleID + 0.0) * INV_TEXTURE_WIDTH) );
		uv1 = ivec2( mod(triangleID + 1.0, 2048.0), floor((triangleID + 1.0) * INV_TEXTURE_WIDTH) );
		uv2 = ivec2( mod(triangleID + 2.0, 2048.0), floor((triangleID + 2.0) * INV_TEXTURE_WIDTH) );
		uv3 = ivec2( mod(triangleID + 3.0, 2048.0), floor((triangleID + 3.0) * INV_TEXTURE_WIDTH) );
		uv4 = ivec2( mod(triangleID + 4.0, 2048.0), floor((triangleID + 4.0) * INV_TEXTURE_WIDTH) );
		uv5 = ivec2( mod(triangleID + 5.0, 2048.0), floor((triangleID + 5.0) * INV_TEXTURE_WIDTH) );
		uv6 = ivec2( mod(triangleID + 6.0, 2048.0), floor((triangleID + 6.0) * INV_TEXTURE_WIDTH) );
		uv7 = ivec2( mod(triangleID + 7.0, 2048.0), floor((triangleID + 7.0) * INV_TEXTURE_WIDTH) );
		
		vd0 = texelFetch(tTriangleTexture, uv0, 0);
		vd1 = texelFetch(tTriangleTexture, uv1, 0);
		vd2 = texelFetch(tTriangleTexture, uv2, 0);
		vd3 = texelFetch(tTriangleTexture, uv3, 0);
		vd4 = texelFetch(tTriangleTexture, uv4, 0);
		vd5 = texelFetch(tTriangleTexture, uv5, 0);
		vd6 = texelFetch(tTriangleTexture, uv6, 0);
		vd7 = texelFetch(tTriangleTexture, uv7, 0);

		// face normal for flat-shaded polygon look
		//intersec.normal = normalize( cross(vec3(vd0.w, vd1.xy) - vec3(vd0.xyz), vec3(vd1.zw, vd2.x) - vec3(vd0.xyz)) );
		//interpolated normal using triangle intersection's uv's
		triangleW = 1.0 - triangleU - triangleV;
		intersec.normal = normalize(triangleW * vec3(vd2.yzw) + triangleU * vec3(vd3.xyz) + triangleV * vec3(vd3.w, vd4.xy));
		intersec.emission = vec3(1, 0, 1); // use this if intersec.type will be LIGHT
		intersec.color = vd6.yzw;
		intersec.uv = triangleW * vec2(vd4.zw) + triangleU * vec2(vd5.xy) + triangleV * vec2(vd5.zw);
		intersec.type = int(vd6.x);
		intersec.albedoTextureID = int(vd7.x);
		//intersec.type = COAT;
		//intersec.albedoTextureID = -1;
	}
	return t;
} // end float SceneIntersect( Ray r, inout Intersection intersec )


//-----------------------------------------------------------------------
vec3 CalculateRadiance( Ray r, inout uvec2 seed ) {
	Intersection intersec;
	Sphere light = spheres[1];
	vec3 accumCol = vec3(0.0);
    vec3 mask = vec3(1.0);
	vec3 checkCol0 = vec3(1.0);
	vec3 checkCol1 = vec3(0.5);

	vec3 dirToLight;
	vec3 tdir;
	
	float epsIntersect = 0.01;
	float nc, nt, Re;
	float weight;
	float diffuseColorBleeding = 0.3; // range: 0.0 - 0.5, amount of color bleeding between surfaces
	
	int diffuseCount = 0;

	bool bounceIsSpecular = true;
	bool sampleLight = false;

	for (int bounces = 0; bounces < 4; bounces++) {
		float t = SceneIntersect(r, intersec);
		if (t == INFINITY) {
			break;
		}
		
		// if we reached something bright, don't spawn any more rays
		if (intersec.type == LIGHT) {	
			//if (bounceIsSpecular)
			accumCol = mask * intersec.emission;
			break;
		}

		if (intersec.type == SPOT_LIGHT) {	
			if (sampleLight) {
				accumCol = mask * intersec.emission;
			}
			else if (bounceIsSpecular && bounces < 2) {
				accumCol = mask * clamp(intersec.emission, 0.0, 5.0);
			}
			break;
		}

		// if we reached this point and sampleLight failed to find a light above, exit early
		if (sampleLight) {
			break;
		}
		
		// useful data 
		vec3 n = intersec.normal;
        vec3 nl = dot(n,r.direction) <= 0.0 ? normalize(n) : normalize(n * -1.0);
		vec3 x = r.origin + r.direction * t;
		
		// Ideal DIFFUSE reflection
        if (intersec.type == DIFF || intersec.type == CHECK) {
			diffuseCount++;
			if ( intersec.type == CHECK ) {
				float q = clamp( mod( dot( floor(x.xz * 0.04), vec2(1.0) ), 2.0 ) , 0.0, 1.0 );
				intersec.color = checkCol0 * q + checkCol1 * (1.0 - q);	
			}
			mask *= intersec.color;
			int id = intersec.albedoTextureID;
			if (id > -1) {
				vec3 albedoSample;
				if (id == 0) albedoSample = texture(tAlbedoTextures[0], intersec.uv).rgb;
				else if (id == 1) albedoSample = texture(tAlbedoTextures[1], intersec.uv).rgb;
				else if (id == 2) albedoSample = texture(tAlbedoTextures[2], intersec.uv).rgb;
				else if (id == 3) albedoSample = texture(tAlbedoTextures[3], intersec.uv).rgb;
				else if (id == 4) albedoSample = texture(tAlbedoTextures[4], intersec.uv).rgb;
				else if (id == 5) albedoSample = texture(tAlbedoTextures[5], intersec.uv).rgb;
				else if (id == 6) albedoSample = texture(tAlbedoTextures[6], intersec.uv).rgb;
				else if (id == 7) albedoSample = texture(tAlbedoTextures[7], intersec.uv).rgb;
				mask *= albedoSample;
			}
			
			/*
			// Russian Roulette - if needed, this speeds up the framerate, at the cost of some dark noise
			float p = max(mask.r, max(mask.g, mask.b));
			if (bounces > 0) {
				if (rand(seed) < p)
                    mask *= 1.0 / p;
                else
                    break;
			}
			*/

			bounceIsSpecular = false;
			if (diffuseCount == 1 && rand(seed) < diffuseColorBleeding) {
                // choose random Diffuse sample vector
				r = Ray( x, randomCosWeightedDirectionInHemisphere(nl, seed) );
				r.origin += nl * epsIntersect;
				continue;
            } else {
				weight = sampleSphereLight(x, nl, dirToLight, light, seed);
				mask *= clamp(weight, 0.0, 1.0);
				r = Ray( x, dirToLight );
				r.origin += nl * epsIntersect;
				sampleLight = true;
				continue;
            }
				
        }
		
		// Ideal SPECULAR reflection
        if (intersec.type == SPEC) {
			mask *= intersec.color;
			r = Ray( x, reflect(r.direction, nl) );
			r.origin += nl * epsIntersect;
			if (bounces > 0)
				bounceIsSpecular = false;
			//bounceIsSpecular = true; // turn on mirror caustics
			continue;
		}
		
		// Ideal dielectric REFRACTION
		if (intersec.type == REFR) {
			nc = 1.0; // IOR of Air
			nt = 1.5; // IOR of common Glass
			Re = calcFresnelReflectance(n, nl, r.direction, nc, nt, tdir);
			if (bounces > 0)
				bounceIsSpecular = false;

			// reflect ray from surface
			if (rand(seed) < Re) {
				r = Ray( x, reflect(r.direction, nl) );
				r.origin += nl * epsIntersect;
				//bounceIsSpecular = true; // turn on reflecting caustics, useful for water
			    continue;	
			} 
			else // transmit ray through surface
			{
				mask *= intersec.color;
				r = Ray(x, tdir);
				r.origin -= nl * epsIntersect;
				bounceIsSpecular = true; 		//turn on refracting caustics
				continue;
			}
			
		} // end if (intersec.type == REFR)
		
		// Diffuse object underneath with ClearCoat on top
        if (intersec.type == COAT) {
			nc = 1.0; // IOR of Air
			nt = 1.4; // IOR of Clear Coat
			Re = calcFresnelReflectance(n, nl, r.direction, nc, nt, tdir);
			// choose either specular reflection or diffuse
			if( rand(seed) < Re ) {	
				r = Ray( x, reflect(r.direction, nl) );
				r.origin += nl * epsIntersect;
				if (bounces > 0)
					bounceIsSpecular = false;
				continue;	
			}

			diffuseCount++;
			mask *= intersec.color;
			bounceIsSpecular = false;
			if (diffuseCount == 1 && rand(seed) < diffuseColorBleeding) {
				// choose random Diffuse sample vector               
				r = Ray( x, randomCosWeightedDirectionInHemisphere(nl, seed) );
				r.origin += nl * epsIntersect;
				continue;
            } else {
				weight = sampleSphereLight(x, nl, dirToLight, light, seed);
				mask *= clamp(weight, 0.0, 1.0);
				r = Ray( x, dirToLight );
				r.origin += nl * epsIntersect;
				sampleLight = true;
				continue;
            }
		} //end if (intersec.type == COAT)
	} // end for (int bounces = 0; bounces < 4; bounces++)
	return accumCol;      
}

//-----------------------------------------------------------------------
void SetupScene(void) {
	vec3 z  = vec3(0);
	vec3 L1 = vec3(0.5, 0.7, 1.0) * 0.2;// Blueish sky light
	vec3 L2 = vec3(1.0, 1.0, 1.0) * 800.0;// Bright white light bulb
	
	spheres[0] = Sphere( 10000.0, vec3(0, 0, 0), L1, z, LIGHT);//large spherical sky light
	spheres[1] = Sphere( 3.0, vec3(-10, 100, -50), L2, z, SPOT_LIGHT);//small spherical light
	spheres[2] = Sphere( 4000.0, vec3(0, -4000, 0), z, vec3(0.4, 0.4, 0.4), CHECK);//Checkered Floor
	vec3 spotLightPos = spheres[1].position;
	vec3 spotLightDir = normalize(vec3(5, 20, -35) - spotLightPos);
	openCylinders[0] = OpenCylinder( spotLightPos - (spotLightDir * spheres[1].radius) * 2.0, spotLightPos + (spotLightDir * spheres[1].radius) * 5.0, 
					   spheres[1].radius * 1.5, z, vec3(1), SPEC);//metal open Cylinder
	disks[0] = Disk( spheres[1].radius * 1.5, spotLightPos - (spotLightDir * spheres[1].radius * 2.0), spotLightDir, z, vec3(0.9, 0.9, 0.9), SPEC);//metal disk
	boxes[0] = Box( vec3(-60.0, 11.0, -110.0), vec3(60.0, 18.0, -20.0), z, vec3(0.2, 0.9, 0.7), REFR);//Glass Box
	boxes[1] = Box( vec3(-40.0, 13.0, -104.0), vec3(40.0, 16.0, -26.0), z, vec3(0, 0, 0), DIFF);//Inner Box
}

// tentFilter from Peter Shirley's 'Realistic Ray Tracing (2nd Edition)' book, pg. 60		
float tentFilter(float x) {
	if (x < 0.5) 
		return sqrt(2.0 * x) - 1.0;
	else 
		return 1.0 - sqrt(2.0 - (2.0 * x));
}

void main( void ) {
	// not needed, three.js has a built-in uniform named cameraPosition
	//vec3 camPos     = vec3( uCameraMatrix[3][0],  uCameraMatrix[3][1],  uCameraMatrix[3][2]);
	vec3 camRight   = vec3( uCameraMatrix[0][0],  uCameraMatrix[0][1],  uCameraMatrix[0][2]);
	vec3 camUp      = vec3( uCameraMatrix[1][0],  uCameraMatrix[1][1],  uCameraMatrix[1][2]);
	vec3 camForward = vec3(-uCameraMatrix[2][0], -uCameraMatrix[2][1], -uCameraMatrix[2][2]);

	// seed for rand(seed) function
	uvec2 seed = uvec2(uFrameCounter, uFrameCounter + 1.0) * uvec2(gl_FragCoord);
	vec2 pixelPos = vec2(0);
	vec2 pixelOffset = vec2(0);
	float x = rand(seed);
	float y = rand(seed);
	if (!uCameraIsMoving) {
		pixelOffset.x = tentFilter(x);
		pixelOffset.y = tentFilter(y);
	}

	// pixelOffset ranges from -1.0 to +1.0, so only need to divide by half resolution
	pixelOffset /= (uResolution * 0.5);
	// we must map pixelPos into the range -1.0 to +1.0
	pixelPos = (gl_FragCoord.xy / uResolution) * 2.0 - 1.0;
	pixelPos += pixelOffset;
	vec3 rayDir = normalize( pixelPos.x * camRight * uULen + pixelPos.y * camUp * uVLen + camForward );
	
	// depth of field
	vec3 focalPoint = uFocusDistance * rayDir;
	float randomAngle = rand(seed) * TWO_PI; // pick random point on aperture
	float randomRadius = rand(seed) * uApertureSize;
	vec3  randomAperturePos = ( cos(randomAngle) * camRight + sin(randomAngle) * camUp ) * randomRadius;
	// point on aperture to focal point
	vec3 finalRayDir = normalize(focalPoint - randomAperturePos);
	Ray ray = Ray( cameraPosition + randomAperturePos , finalRayDir );
	SetupScene();
	
	// perform path tracing and get resulting pixel color
	vec3 pixelColor = CalculateRadiance( ray, seed );
	vec3 previousColor = texelFetch(tPreviousTexture, ivec2(gl_FragCoord.xy), 0).rgb;
	if ( uCameraJustStartedMoving ) {
		previousColor = vec3(0.0); // clear rendering accumulation buffer
	} else if ( uCameraIsMoving ) {
		previousColor *= 0.5; // motion-blur trail amount (old image)
		pixelColor *= 0.5; // brightness of new image (noisy)
	}
	out_FragColor = vec4( pixelColor + previousColor, 1.0 );
}